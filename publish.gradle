apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username properties.getProperty('sonatypeUsername')
                password properties.getProperty('sonatypePassword')
            }
        }
    }

    publications {
        mavenPublication(MavenPublication) {
            from components.java
             artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.advancedpwr'
            artifactId 'thor'
            version project.version
            
            pom {
                name = 'Thor'
                description = 'Test Harness Object Recorder for Java'
                url = 'https://advancedpwr.com/thor'
                
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                developers {
                    developer {
                        id = 'avery1701'
                        name = 'Matthew Avery'
                        email = 'mavery@advancedpwr.com'
                    }
                    developer {
                        id = 'mrzeusthecoder'
                        name = 'Elijah Hopp'
                        email = 'github.mrzeusthecoder@gmail.com'
                    }
                }
                
                scm {
                    connection = 'scm:git:https://github.com/avery1701/thor.git'
                    url = 'https://github.com/avery1701/thor.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenPublication
}